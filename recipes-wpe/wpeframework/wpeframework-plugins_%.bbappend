# Enable TV Control in WPE Framework plugins

# Linuv TV settings
FREQUENCY_LIST      ?= "575"
COUNTRY_REGION_ID   ?= "0"
COUNTRY_CODE        ?= "GBR"

# GENERIC Settings
TUNE_PARAM          ?= "SYMBOL_RATE=6900000"

TVCONTROL_PARAMS = "\
    -DWPEFRAMEWORK_PLUGINS_TVCONTROL_FREQUENCY_LIST=${FREQUENCY_LIST} \
    -DWPEFRAMEWORK_PLUGINS_TVCONTROL_COUNTRY_REGION_ID=${COUNTRY_REGION_ID} \
    -DWPEFRAMEWORK_PLUGINS_TVCONTROL_COUNTRY_CODE=${COUNTRY_CODE} \
    -DWPEFRAMEWORK_PLUGINS_TVCONTROL_TUNE_PARAM=${TUNE_PARAM} \
"

PACKAGECONFIG_append = " \
    dvb \
    ${@bb.utils.contains('PREFERRED_PROVIDER_virtual/egl', 'broadcom-refsw', 'tvcontrol_bcm', 'tvcontrol_linuxtv', d)} \
"

# BROADCOM
PACKAGECONFIG[tvcontrol_bcm] = " \
    -DWPEFRAMEWORK_PLUGIN_TVCONTROL=ON \
    ${TVCONTROL_PARAMS} \
    ,,wpetvplatform-bcm \
"

# LINUX TV
PACKAGECONFIG[tvcontrol_linuxtv] = " \
    -DWPEFRAMEWORK_PLUGIN_TVCONTROL=ON \
    -DPLUGIN_TVCONTROL_LINUXTV=ON \
    ${TVCONTROL_PARAMS} \
    ,,sqlite,inotify-tools gstreamer1.0-plugins-bad-dvb gstreamer1.0-libav \
"

# GENERIC
PACKAGECONFIG[dvb]              = "-DWPEFRAMEWORK_PLUGIN_TVCONTROL_DVB=true,-DWPEFRAMEWORK_PLUGIN_TVCONTROL_DVB=false,,dvb-apps"
PACKAGECONFIG[bouquet_parsing]  = "-DWPEFRAMEWORK_PLUGIN_TVCONTROL_ENABLE_BOUQUET_PARSING=true,,"
